plugins {
    id 'java'
    id 'idea'
}

def author = 'LazoYoung'
def classpath = 'com.naver.idealproduction.Main'
def desc = 'A simple plugin that calls crop diseases in Minecraft.'
group = 'com.naver.idealproduction'
version = '1.1'
sourceCompatibility = JavaVersion.VERSION_11
archivesBaseName = 'CropPlusPlus'

idea.module {
    downloadJavadoc = true
    downloadSources = true
}

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'utf-8'
}

test {
    useJUnitPlatform()
}

processResources {
    include '**/*.yml'
    expand(version: version, classpath: classpath, desc: desc, author: author, name: project.name)
    filteringCharset('utf-8')
}